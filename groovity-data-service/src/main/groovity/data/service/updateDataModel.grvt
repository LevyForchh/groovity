/*******************************************************************************
 * Â© 2018 Disney | ABC Television Group
 *
 * Licensed under the Apache License, Version 2.0 (the "Apache License")
 * with the following modification; you may not use this file except in
 * compliance with the Apache License and the following modification to it:
 * Section 6. Trademarks. is deleted and replaced with:
 *
 * 6. Trademarks. This License does not grant permission to use the trade
 *     names, trademarks, service marks, or product names of the Licensor
 *     and its affiliates, except as required to comply with Section 4(c) of
 *     the License and to reproduce the content of the NOTICE file.
 *
 * You may obtain a copy of the Apache License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the Apache License with the above modification is
 * distributed on an "AS IS" BASIS, WITHOUT WARRANTIES OR CONDITIONS OF ANY
 * KIND, either express or implied. See the Apache License for the specific
 * language governing permissions and limitations under the Apache License.
 *******************************************************************************/
import com.disney.groovity.data.service.MultipartModel
import com.disney.groovity.servlet.error.GroovityError

static conf=[
	'groovity.data.service.buffer': '2MB'
]

static web=[
	path:'/data/{type}/{id}',
	methods:['PUT','POST'],
	inputs:['application/json','application/xml', 'multipart/form-data'],
	outputs:['application/json','application/xml'],
	charset:'UTF-8',
	auth:[ policy: { run('/data/service/policy') } ],
	cors:[ policy: { run('/data/service/policy') } ],
	buffer:{ conf['groovity.data.service.buffer'] }
]

load '/data/factory'

def dataModel = null
if(request.method == 'POST'){
	//POST requests allow partial updates so load the prior state for merge; 
	//PUT must be a full representation per HTTP Semantics so then discard prior state
	factory.invalidate(type, id)
	dataModel = factory(type, id)
}
if(dataModel == null){
	dataModel = factory(type)
}

if(!(dataModel instanceof Stored)){
	response.sendError(400,'DataModel cannot be Stored')
	return
}
try{
	if(request.getHeader('Content-Type').startsWith('multipart/form-data')){
		MultipartModel.copy(request.parts,dataModel)
	}
	else{
		parse(value: request, to: dataModel)
	}
}
catch(Exception e){
	log(warn:"Error parsing data",thrown:e)
	response.sendError(400, GroovityError.describe(e))
	return
}
dataModel.getPointer().id = id
dataModel.store()

write(value: factory(dataModel.getPointer()), filter: Attachment.DESCRIBE)